plugins {
  id 'java-library'
  id 'org.jetbrains.kotlin.jvm'
  id 'maven-publish'
  id 'signing'
  id ("org.danilopianini.publish-on-central") version "9.0.7"
}

// Loads versions from gradle.properties
def gdxVersion = project.properties['gdxVersion']
def box2dlightsVersion = project.properties['box2dlightsVersion']
def ktxVersion = project.properties['ktxVersion']

sourceSets {
  main {
    kotlin {
      srcDirs = ['src/main/kotlin']
      exclude 'com/github/bennyOe/gdxNormalLight/demo/**',
              'com/github/bennyOe/gdxNormalLight/GgdxNormalMapExample.kt'
    }
    resources {
      srcDirs = ["src/main/resources", "${project.rootDir}/assets"]
      include "shader/**"
    }
    java.srcDirs += kotlin.srcDirs
  }
}

tasks.jar {
    dependsOn classes
    dependsOn processResources
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    exclude 'com/github/bennyOe/gdxNormalLight/demo/**'
    exclude 'com/github/bennyOe/gdxNormalLight/GgdxNormalMapExample.kt'
}

publishOnCentral {
  repoOwner.set("bennyOe")
  projectDescription.set("A 2D Normal Mapping & Light engine for LibGDX.")
  projectLongName.set(project.name)
  licenseName.set("Apache License, Version 2.0")
  licenseUrl.set("http://www.apache.org/licenses/LICENSE-2.0")
  projectUrl.set("https://github.com/${repoOwner.get()}/${project.name}")
  scmConnection.set("scm:git:https://github.com/${repoOwner.get()}/${project.name}")
}

dependencies {
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  api "com.badlogicgames.box2dlights:box2dlights:$box2dlightsVersion"
  api "io.github.libktx:ktx-app:$ktxVersion"
  api "io.github.libktx:ktx-assets:$ktxVersion"
  api "io.github.libktx:ktx-math:$ktxVersion"
  api "io.github.libktx:ktx-async:$ktxVersion"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'io.github.bennyoe'
      artifactId = 'gdx-normal-light'

      artifact jar
      artifact sourcesJar


      pom {
        name = 'GDX Normal Light'
        description = 'A 2D Normal Mapping & Light engine for LibGDX.'
        url = 'https://github.com/bennyOe/gdx-normal-light'
        licenses {
          license {
            name = 'Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0'
          }
        }
        developers {
          developer {
            id = 'bennyOe'
            name = 'Benjamin Oechsle'
            email = 'b.oechsle@proton.me'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/bennyOe/gdx-normal-light.git'
          developerConnection = 'scm:git:ssh://github.com:bennyOe/gdx-normal-light.git'
          url = 'https://github.com/bennyOe/gdx-normal-light'
        }
      }
    }
  }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava

    def signingPassword = project.findProperty("signing.password")
    if (signingPassword) {
        password = signingPassword
    }
}
