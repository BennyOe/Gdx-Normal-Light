plugins {
  id 'java-library'
  id 'org.jetbrains.kotlin.jvm'
  id 'maven-publish'
  id 'signing'
}

// Loads versions from gradle.properties
def gdxVersion = project.properties['gdxVersion']
def box2dlightsVersion = project.properties['box2dlightsVersion']
def ktxVersion = project.properties['ktxVersion']

sourceSets {
  main {
    kotlin {
      srcDirs = ['src/main/kotlin']
      exclude 'com/github/bennyOe/gdxNormalLight/demo/**',
              'com/github/bennyOe/gdxNormalLight/GgdxNormalMapExample.kt'
    }
    resources {
      srcDirs = ["src/main/resources", "${project.rootDir}/assets"]
      exclude("textures/**", "assets.txt")
    }
    java.srcDirs += kotlin.srcDirs
  }
}

tasks.jar {
    dependsOn classes
    dependsOn processResources
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    exclude 'com/github/bennyOe/gdxNormalLight/demo/**'
    exclude 'com/github/bennyOe/gdxNormalLight/GgdxNormalMapExample.kt'
}

dependencies {
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  api "com.badlogicgames.box2dlights:box2dlights:$box2dlightsVersion"
  api "io.github.libktx:ktx-app:$ktxVersion"
  api "io.github.libktx:ktx-assets:$ktxVersion"
  api "io.github.libktx:ktx-math:$ktxVersion"
  api "io.github.libktx:ktx-async:$ktxVersion"
}

// This task creates a JAR containing the source code.
tasks.register('sourcesJar', Jar) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'io.github.bennyoe'
      artifactId = 'gdx-normal-light'
      version = '1.0.0'

      artifact jar
      artifact sourcesJar


      pom {
        name = 'GDX Normal Light'
        description = 'A 2D Normal Mapping & Light engine for LibGDX.'
        url = 'https://github.com/bennyOe/gdx-normal-light'
        licenses {
          license {
            name = 'The MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'bennyOe'
            name = 'Benjamin Oechsle'
            email = 'b.oechsle@proton.me'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/bennyOe/gdx-normal-light.git'
          developerConnection = 'scm:git:ssh://github.com:bennyOe/gdx-normal-light.git'
          url = 'https://github.com/bennyOe/gdx-normal-light'
        }
      }
    }
  }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava

    def signingPassword = project.findProperty("signing.password")
    if (signingPassword) {
        password = signingPassword
    }
}
